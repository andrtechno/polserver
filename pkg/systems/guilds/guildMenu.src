/*
        Maintened by The POL Distro Team

        2009-05-30
*/
use uo;
use guilds;

include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":guilds:guild";
include ":guilds:settings";
include ":guilds:validate";
include "include/security";
include "include/string";

var toggle_menu := 0;
var settings := Guild_GetSettingsCfgElem( "Settings" );

program Guild_Create( parms )

    var mobile,
        viewer,
        guild_id := 0;

	if( Lower( TypeOf( parms )) == "array" )
        mobile := parms[1];
        viewer := parms[2];
        if( Lower( TypeOf( parms[2] )) == "integer" )
            guild_id := parms[2];
            viewer := mobile;
        endif
    else
        mobile := parms;
        viewer := parms;
	endif

    if( GetProcess( CInt( GetObjProperty( mobile, "#GuildButton" ))))
		return 0;
	else
		SetObjProperty( mobile, "#GuildButton", GetPid() );
	endif

	showGuildInfo( mobile, viewer, guild_id );

    return 1;
endprogram

function showGuildInfo( mobile, viewer, guild_id := 0 )

    var temp_guild;
    if( !guild_id )
        temp_guild := mobile.guildid;
    else
        temp_guild := guild_id;
    endif

    var guild := FindGuild( temp_guild );
    if( guild.errortext )
        SendSysMessage( mobile, "Error cant find guild -->"+guild.errortext );
        return 0;
    endif

	var gump := GFCreateGump();

	GFResizePic( gump, 0, 0, GFGetCfgConst( "Defaults", "BackGround" ), 400, 520 );
    GFResizePic( gump, 15, 15, GFGetCfgConst( "Defaults", "ForeGround" ), 370, 25 );
	GFResizePic( gump, 15, 45, GFGetCfgConst( "Defaults", "ForeGround" ), 370, 465 );
	GFTextMid( gump, 10, 17, 400, 1720, "Guild Menu for "+guild.GetProp( "Name" ) );
	GFTextRight( gump, -10, 17, 3, "Points "+CInt(guild.GetProp( "Point" )) );


	GFAddButton( gump, 100, 470, 2119, 2120, GF_CLOSE_BTN, 0xA2 ); // Cancel
	GFAddButton( gump, 25, 470, 2128, 2129, GF_CLOSE_BTN, 0xA1 ); // Okay

    var temp_charter := guild.GetProp( "Charter" );
    var text_charter;

    foreach line in temp_charter
        if( _line_iter == 1 )
            text_charter := CStr( line );
        else
            text_charter += " "+CStr( line );
        endif

        SleepMS(5);
    endforeach

    if( !text_charter )
        text_charter := "No charter.";
    endif

    var guild_icon := guild.GetProp( "GuildIcon" );
    if( !guild_icon )
        guild_icon := 0x15a9;
    endif

    GFGumpPic( gump, 295, 430, guild_icon );



    if( toggle_menu )
        if( mobile.serial == guild.GetProp( "Master" ) || viewer.cmdlevel >= 3 )
            showGuildMasterInfo( mobile, gump, guild, viewer );
        else
            showGuildMemberInfo( mobile, gump, guild, viewer );
        endif
    else
    	GFResizePic( gump, 30, 60, GFGetCfgConst( "Backgrounds", "GOLDTRIM" ), 337, 100 );
    	GFHTMLArea( gump, 40, 70, 326, 80, text_charter, 0, 1 );
        showGuildMemberInfo( mobile, gump, guild, viewer );
    endif

    return 1;
endfunction

function showGuildMemberInfo( mobile, byref gump, guild, viewer )

    GFTextLine( gump, 55, 180, 2100, "Посмотреть текущий список гильдии." ); //View the current guild roster
    GFRadioButton( gump, 30, 180, 210, 211, 0, 0xb00 );
    if(guild.GetProp("Master") == viewer.serial)
        GFTextLine( gump, 55, 220, 2100, "Пригласить в гильдию." ); //Recruit someone into the guild
        GFRadioButton( gump, 30, 220, 210, 211, 0, 0xb01 );
    endif
    GFTextLine( gump, 55, 240, 2100, "Посмотреть список спонсируемых рекрутов." ); //View list of sponsored recruits
    GFRadioButton( gump, 30, 240, 210, 211, 0, 0xb02 );

    GFTextLine( gump, 55, 280, 2100, "Посмотреть список ваших союзников." ); //View list of your allies
    GFRadioButton( gump, 30, 280, 210, 211, 0, 0xb03 );

    GFTextLine( gump, 55, 300, 2100, "Посмотреть список ваших врагов." ); //View list of your enemies
    GFRadioButton( gump, 30, 300, 210, 211, 0, 0xb04 );

    GFTextLine( gump, 55, 320, 2100, "Посмотреть все гильдии.." ); //View all guilds.
    GFRadioButton( gump, 30, 320, 210, 211, 0, 0xb05 );


    GFTextLine( gump, 55, 360, 2100, "Вкл/Выкл. название своей гильдии." ); //Toggle your guild title on or off.
    GFRadioButton( gump, 30, 360, 210, 211, 0, 0xb06 );

    if( GetObjProperty( mobile, "Guild_Abbr" ))
        GFTextLine( gump, 340, 360, 70, "Вкл." );
    else
        GFTextLine( gump, 340, 360, 33, "Выкл." );
    endif

    GFTextLine( gump, 55, 380, 2100, "Выйти из гильдии." ); //Resign from the guild.
    GFRadioButton( gump, 30, 380, 210, 211, 0, 0xb07 );

    GFTextLine( gump, 55, 400, 2100, "Создать форму гильдии." ); //Create a guild form.
    GFRadioButton( gump, 30, 400, 210, 211, 0, 0xb09 );

    if(guild.GetProp("Master") == viewer.serial)
        GFTextLine( gump, 55, 420, 2100, "Функции мастера гильдии." ); //Guild Master functions.
        GFRadioButton( gump, 30, 420, 210, 211, 0, 0xb08 );
    endif
    var input := GFSendGump( viewer, gump );
    if( !input || input[0xA2] )
		SendSysMessage( viewer, "Cancelled.", 3, 33 );
		return 0;
    elseif( input[0xb00] )
        ViewRosters( mobile, guild, viewer );
        return 1;
    elseif( input[0xb01] )
        RecruitAPlayer( mobile, guild, viewer );
        return 1;
    elseif( input[0xb02] )
        ViewRecruits( mobile, guild, viewer );
        return 1;
    elseif( input[0xb03] )
        ViewAllies( mobile, guild, viewer );
        return 1;
    elseif( input[0xb04] )
        ViewEnemies( mobile, guild, viewer );
        return 1;
    elseif( input[0xb05] )
        PendingStatus( mobile, guild, viewer );
        return 1;
    elseif( input[0xb06] )
        ToggleTitle( mobile, guild, viewer );
        return 1;
    elseif( input[0xb07] )
        Resign( mobile, guild, viewer );
        return 1;
    elseif( input[0xb08] )
        toggle_menu := 1;
        showGuildInfo( mobile, viewer );
        return 1;
    elseif( input[0xb09] )
        toggle_menu := 1;
        MakeForm( mobile, guild, viewer );
        return 1;
    endif
endfunction

function showGuildMasterInfo( mobile, byref gump, guild, viewer )

    GFTextLine( gump, 55, 60, 2100, "Установить устав гильдии." ); //Set the guild charter.
    GFRadioButton( gump, 30, 60, 210, 211, 0, 0xc00 );

    GFTextLine( gump, 55, 80, 2100, "Поместите камень гильдии." ); //Place the GuildStone.
    GFRadioButton( gump, 30, 80, 210, 211, 0, 0xc01 );

    GFTextLine( gump, 55, 120, 2100, "Сменить название гильдии. ("+CInt( settings.RegistrationFee )+" gp)" ); //Change the guild name
    GFRadioButton( gump, 30, 120, 210, 211, 0, 0xc02 );

    GFTextLine( gump, 55, 140, 2100, "Сменить значок гильдии." ); //Change the guild icon.
    GFRadioButton( gump, 30, 140, 210, 211, 0, 0xc04 );

    GFTextLine( gump, 55, 180, 2100, "Форма гильдии" );
    GFRadioButton( gump, 30, 180, 210, 211, 0, 0xc10 );

    GFTextLine( gump, 55, 200, 2100, "Магазин гильдии" );
    GFRadioButton( gump, 30, 200, 210, 211, 0, 0xc11 );

    GFTextLine( gump, 55, 220, 2100, "Установить цвет гильд стоуна." );
    GFRadioButton( gump, 30, 220, 210, 211, 0, 0xc12 );

    GFTextLine( gump, 55, 240, 2100, "Подтвердить союз" );
    GFRadioButton( gump, 30, 240, 210, 211, 0, 0xc13 );
    GFTextLine( gump, 55, 260, 2100, "Подтвердить войну" );
    GFRadioButton( gump, 30, 260, 210, 211, 0, 0xc14 );

    GFTextLine( gump, 55, 300, 2100, "Предоставить титул другому члену." ); //Grant title to another member.
    GFRadioButton( gump, 30, 300, 210, 211, 0, 0xc03 );

    GFTextLine( gump, 55, 320, 2100, "Ожидание подтверждение новичка." ); //Pending recruit seeking membership.
    GFRadioButton( gump, 30, 320, 210, 211, 0, 0xc05 );

    GFTextLine( gump, 55, 340, 2100, "Убрать участника." ); //Dismiss a member.
    GFRadioButton( gump, 30, 340, 210, 211, 0, 0xc06 );

    GFTextLine( gump, 55, 380, 2100, "Объявить войну другой гильдии." ); //Declare war against another guild.
    GFRadioButton( gump, 30, 380, 210, 211, 0, 0xc07 );

    GFTextLine( gump, 55, 400, 2100, "Объявите мир другой гильдии." ); //Declare peace towards another guild
    GFRadioButton( gump, 30, 400, 210, 211, 0, 0xc08 );

    GFTextLine( gump, 55, 440, 2100, "Передача лидерства" ); //Transfer Leadership
    GFRadioButton( gump, 30, 440, 210, 211, 0, 0xc09 );

    var input := GFSendGump( viewer, gump );
    if( !input || input[0xA2] )
        toggle_menu := 0;
        showGuildInfo( mobile, viewer );
        return 1;
    elseif( input[0xc00] )
        SetCharter( mobile, guild, viewer );
        return 1;
    elseif( input[0xc01] )
        PlaceGuildStone( mobile );
        return 1;
    elseif( input[0xc02] )
        ChangeGuildName( mobile, guild, viewer );
        return 1;
    elseif( input[0xc03] )
        GrantTitle( mobile, guild, viewer );
        return 1;
    elseif( input[0xc04] )
        ChangeGuildIcon( mobile, guild, viewer );
        return 1;
    elseif( input[0xc05] )
        RecruitStatus( mobile, guild, viewer );
        return 1;
    elseif( input[0xc06] )
        DismissMember( mobile, guild, viewer );
        return 1;
    elseif( input[0xc07] )
        DeclareWar( mobile, guild, viewer );
        return 1;
    elseif( input[0xc08] )
        DeclarePeace( mobile, guild, viewer );
        SendSysMessage( mobile, "Option not implemented yet, but we are working on it.", 3, 68 );
        return 1;
    elseif( input[0xc09] )
        TransferLeader( mobile, guild, viewer );
        return 1;
    elseif( input[0xc10] )
        MakeGuildCloth( mobile, guild, viewer );
        return 1;
    elseif( input[0xc11] )
        SendSysMessage( mobile, "Guild shop. (dev)", 3, 68 );
        return 1;
    elseif( input[0xc12] )
        setStoneColor(mobile, guild);
        return 1;
    elseif( input[0xc13] )
        PeaceAccept(mobile, guild, viewer);
        //SendSysMessage( mobile, "Option not implemented yet, but we are working on it. 13", 3, 68 );
    elseif( input[0xc14] )
        //SendSysMessage( mobile, "Option not implemented yet, but we are working on it. 14", 3, 68 );
        WarAccept(mobile, guild, viewer);
    else
        toggle_menu := 0;
        showGuildInfo( mobile, viewer );
        return 1;
    endif
endfunction
