//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// PKG: housing                                                                                                 //
// version: 1.0                                                                                                 //
// Maintainer: Bishop Ebonhand                                                                                  //
// email: bish0p@cfl.rr.com                                                                                     //
//                                                                                                              //
// notes:                                                                                                       //
//   this housing package includes static housing as well as the typical multi type housing.                    //
//                                                                                                              //
// known issues:                                                                                                //
//  stacking "solid" objects causes some wierdness with the multi.items list, and as a result a player may      //
//  not be able to lock down items on tables within their homes.. when a fix or workaround becomes available    //
//  I will update this script accordingly...                                                                    //
//                                                                                                              //
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
use os;
use uo;


program use_securecont( character, item )
    var signserial := GetObjProperty( item, "signserial");
    var sign      := SystemFindObjectBySerial( signserial );
    var secure := 0;
    if (GetObjProperty( sign, "owneracct" ) == character.acctname)
      secure := 1;
    else
      var friendinfo := GetObjProperty(sign,"friends");
      var coownerinfo := GetObjProperty(sign,"coowners");
      var ind := returnindex(friendinfo, character.serial);
      if (ind > 0)
        secure := 1;
        SendSysMessage( character, "opening container for owner or friend." );
      else
        ind := returnindex(coownerinfo, character.serial);
        if (ind > 0)
          secure := 1;
          SendSysMessage( character, "opening container for owner or friend." );
        elseif (character.cmdlevel > 2)
          secure := 1;
          SendSysMessage( character, "opening container for staff member." );
        endif
      endif
    endif
    if (secure == 0)
      SendSysMessage( character, "You can't use this." );
      return;
    endif
    var boxin := ListRootItemsInContainer(item);
    var boxs := GetObjProperty( item, "boxserial" );
    var boxo := SystemFindObjectBySerial(boxs);
    SendOpenSpecialContainer( character, boxo);
    foreach thingie in boxin
      if (thingie.serial != boxs)
        MoveItemToContainer(thingie, boxo);
      endif
    endforeach
    sleep(1);
endprogram

function returnindex(friendinfo, information)
  var i;
  for (i := 1; i <= len(friendinfo); i := i + 1)
  if (friendinfo[i] == information)
    return i;
    endif
  endfor
  return 0;
endfunction

function ListRootItemsInContainer(container)
  var ret := { };
  foreach item in EnumerateItemsInContainer(container)
    if ( item.container.serial == container.serial )
	  ret[len(ret)+1] := item;
    endif
  endforeach
  return ret;
endfunction