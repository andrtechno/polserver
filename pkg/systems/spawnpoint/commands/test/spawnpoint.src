use uo;


include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":spawnpoint:spawn";
include "include/client";

program textcnd_spawnpoint(who)

    var gump := GFCreateGump ( 100, 100 );
    GFPage ( gump, 0);
    GFResizePic(gump, 0, 0, GetCfgConst("Defaults", "ForeGround"), 190, 140);
    GFTextLine(gump, 15, 15, 2100, "SpawnPoint System" );

    var paddingTop:=20;
    var stepTop:=20;


    GFTextLine(gump, 15, paddingTop + stepTop, 67, "Удалить спауны");
    GFAddButton(gump, 140, paddingTop + stepTop, 4023, 4024, GF_CLOSE_BTN, 1);
    stepTop += 25;
    GFTextLine(gump, 15, paddingTop + stepTop, 67, "Сохранить спауны");
    GFAddButton(gump, 140, paddingTop + stepTop, 4023, 4024, GF_CLOSE_BTN, 2);
    stepTop += 25;
    GFTextLine(gump, 15, paddingTop + stepTop, 67, "CreateSpawns");
    GFAddButton(gump, 140, paddingTop + stepTop, 4023, 4024, GF_CLOSE_BTN, 3);

    var input := GFSendGump ( who, gump);
    input := CInt(input[0]);

    case(input)
        1: RemoveAll(who);
        2: SaveAll(who);
        3: CreateSpawns(who);
        default: SendSysMessage(who, "Cancelled.", 3, MESSAGE_COLOR_BAD);
    endcase

    return 1;
endprogram

function SaveAll(who)
	var x := 0;

    foreach thing in ListItemsInBoxOfObjType( 0x1A401, 0, 0, -100, 6143, 4095, 100, who.realm )

        var minDelay := CInt(GetObjProperty(thing, "MinDelay"));
        var maxDelay := CInt(GetObjProperty(thing, "MaxDelay"));
        var range := CInt(GetObjProperty(thing, "Range"));
        var template := CStr(GetObjProperty(thing, "Spawn"));
        var amount := CInt(GetObjProperty(thing, "Amount"));

        var data := array;
        var holder := array;

        holder.+pname  := "Spawn";
        holder.+pvalue := template;
        data.append(holder);

        holder.pname  := "Coord";
        holder.pvalue := thing.x + " " +thing.y+ " "+thing.z;
        data.append(holder);

        holder.pname  := "Realm";
        holder.pvalue := thing.realm;
        data.append(holder);

        holder.pname  := "MinDelay";
        holder.pvalue := minDelay;
        data.append(holder);

        holder.pname  := "MaxDelay";
        holder.pvalue := maxDelay;
        data.append(holder);

        holder.pname  := "Range";
        holder.pvalue := range;
        data.append(holder);

        holder.pname  := "Amount";
        holder.pvalue := amount;
        data.append(holder);

        print(data);
        AppendConfigFileElem(":spawnpoint:setup-britannia", "Spawn", x, data);
        x := x + 1;
    endforeach

endfunction

function RemoveAll(who)
    foreach thing in ListItemsInBoxOfObjType( 0x1A401, 0, 0, -100, 6143, 4095, 100, who.realm )
        DestroyItem(thing);
    endforeach
endfunction

function CreateSpawns(unused who)
    CreateNpcSpawn();
endfunction
