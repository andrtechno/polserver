use datafile;
use cfgfile;
use file;
use os;
use uo;

/**********************************************************
*
*
*	Sysbook Package - Start.src
*	The purpose of this start script is to go and generate a
*	datastore from the :sysbook:books.cfg file which contains 
*	which contains a record of all the different types of books
*	that can be generated by the system.
*
*	Spawning of bookshelves are now controlled by control scripts
*	on the shelves themselves.
*
*
\**********************************************************/

program InitBookDatastore()
	print("Sysbook: Updating book datastore");
  var i,j,pages,entry,title,author,counter;
  var bookstr := array{};
  var books:= ReadConfigFile("books");
  if(!books)
  	LogToFile("::log/sysbook.log", "[start.ecl]: Error loading config file: " + books.errortext, LOG_DATETIME);
  	print("Sysbook error - check sysbook.log");
  	return 0;
  endif
  var thebooks:=GetConfigStringKeys(books);
  var df := CreateDataFile("staticbooks", DF_KEYTYPE_INTEGER);
  if(!df)
  	LogToFile("::log/sysbook.log", "[start.ecl]: Datastore file error: " + df.errortext, LOG_DATETIME);
  	print("Sysbook error - check sysbook.log");
  	return 0;
  endif
  print("thebooks size is " + thebooks.size());
  foreach bookitem in thebooks
    bookitem:=CInt(bookitem);
    if(!BookExists(df,bookitem))
      counter:=1;
      pages:=CInt(books[bookitem].pages);
      title:=(books[bookitem].title);
      author:=(books[bookitem].author);
      for(i:=1;i<=pages;i:=i+1)
        for(j:=1;j<=8;j:=j+1)
          entry:=GetConfigString(books[bookitem], "p"+i+"l"+j);
          if(!entry.errortext)
            bookstr[counter]:=entry;
          else
            bookstr[counter]:=" ";
          endif
          counter:=counter+1;
        endfor
      endfor
      CreateBook(df,bookitem,title,author,bookstr);
    endif
  endforeach
  UnloadConfigFile("books");
endprogram


function BookExists(df, bookid)
  return(df.FindElement(bookid) != error);
endfunction

function CreateBook(df, bookid, title, author, contents)
  var elem := df.CreateElement(bookid);
  elem.SetProp("author", author);
  elem.SetProp("title", title);
  elem.SetProp("contents", contents);
endfunction