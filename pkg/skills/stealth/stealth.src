
use uo;
use util;
use cliloc;

include "include/client";
include ":attributes:attributes";
include "include/trainingskill";

program skill_stealth( who )
    var data := {};
    data[1] := "stealth";
    data[2] := ReadGameClock();
    data[3] := 47;
    SetObjProperty(who, "LastUsedSkill", data );
    TrainingSkill(who, data[3]);

    EraseObjProperty(who, "IsMeditating");
    EraseObjProperty(who, "HealTimer");
    if(GetAttribute(who, HIDING) < 80)
        who.hidden := 0;
        PrintTextAbovePrivateCL(who, who, 502726, color := MESSAGE_COLOR_BAD); // You are not hidden well enough.  Become better at hiding.
        return;
    endif
    if(!who.hidden)
        PrintTextAbovePrivateCL(who, who, 502725, color := MESSAGE_COLOR_BAD); // You must hide first (вы должны быть под свойством hidden.)
        return;
    endif
    var mount := GetEquipmentByLayer(who, LAYER_MOUNT );
    if(mount)
        //who.hidden := 0;
        //PrintTextAbovePrivateCL(who, who, 1061089, color := MESSAGE_COLOR_BAD); // You must dismount first.
        //return;

        var animal := SystemFindObjectBySerial(CInt(GetObjProperty(mount, "serial")));
        if(animal.npctemplate != "stealthhorse")
            who.hidden := 0;
            PrintTextAbovePrivate( who, "You cant stealth while riding a horse!", who );
            return;
        endif

    endif
    var skill := CInt(GetAttribute(who, STEALTH) / 5);

    if(skill > 35)
        skill := 35;
    endif
    if(SkillCheck( who, STEALTH, -1, skill ) > 0)
        //skill := (skill / 10);
        //who.stealthsteps := skill + RandomInt(skill);
        skill := (skill + RandomInt(skill));


        var premium := who.IsPremium();
        case(premium)
            1: skill += 2;
            2: skill += 5;
            3: skill += 8;
        endcase


        who.stealthsteps := skill;
        //PrintTextAbovePrivateCL(who, who, 502730, color := 66); // You begin to move quietly.
        PrintTextAbovePrivate( who, "You are now moving "+who.stealthsteps+" steps", who, color:=MESSAGE_COLOR_GOOD);
    else
        who.hidden := 0;
        PrintTextAbovePrivateCL(who, who, 500814, color := MESSAGE_COLOR_BAD); // You have been revealed!
  endif
endprogram
