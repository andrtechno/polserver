use uo;

include ":attributes:attributes";
include ":magery:spells";
include "include/client";
include "include/sounds";

program mana_drain ( parms )
    var caster := parms[1];
    var info := parms[2];
    var cast_on;
    cast_on := MS_Target(caster, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HARMFUL);
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := parms[4];
    endif
    if (!cast_on)
        SendSysMessage (caster, "Cancelled.", color := 33);
        return 0;
    endif
    if ( !MS_MobileCheck(caster, cast_on) )
        return 0;
    endif

  SetObjProperty(cast_on, "LastHit", {caster.name, caster.serial, "mana vampire"});

// Using POL 0.95 Distro code here

 if(Reflected(cast_on))
	var tmp := cast_on;
        SendSysMessage (caster, "The spell was reflected back onto you!!!", color := 33);
	cast_on := caster;
        caster := tmp;
  endif
  if(!cast_on.isA(POLCLASS_MOBILE))
    SendSysMessage (caster, "That thing doesn't have any mana.");
    return;
  endif
  var dmg := Cint(GetAttribute(caster, "magery") / 3);
  var diff := Resisted(info.circle, caster, cast_on, dmg);
  if(diff)
    PlaySoundEffect(caster, SFX_SPELL_MANA_VAMPIRE);
    PlayObjectCenteredEffect(cast_on, FX_GLOW, 7,0x10);
    var gain := 0;
	if(diff > GetMana(cast_on))
	  gain := GetMana(cast_on);
	  SetMana(cast_on, 0);
	else
	  gain := diff;
	  SetMana(cast_on, GetMana(cast_on) - diff);
	endif
	SetMana(caster, GetMana(caster) + gain);
	if(GetMana(caster) > GetMaxMana(caster))
	  SetMana(caster, GetMaxMana(caster));
	endif
  endif

  return 1;
endprogram