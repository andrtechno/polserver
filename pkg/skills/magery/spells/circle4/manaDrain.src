use uo;
use util;

include ":attributes:attributes";
include ":magery:spells";
include "include/client";
include "include/sounds";


program mana_drain ( parms )
    var caster := parms[1];
    var info := parms[2];
    var cast_on;
    cast_on := MS_Target(caster, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HARMFUL);
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := parms[4];
    endif
    if (!cast_on)
        SendSysMessage (caster, "Cancelled.", color := 33);
        return 0;
    endif
    if ( !MS_MobileCheck(caster, cast_on) )
        return 0;
    endif
    
    PlaySoundEffect( caster, SFX_SPELL_MANA_DRAIN );
    PlayObjectCenteredEffect( cast_on, FX_HEAL_EFFECT, 7,0x10 );

    SetObjProperty(cast_on, "LastHit", {caster.name,caster.serial, "mana drain" });
        if(Reflected(cast_on))
            SendSysMessage (caster, "The spell was reflected back onto you!!!", color := 33);
            var tmp := cast_on;
            cast_on := caster;
            caster := tmp;
        endif
        var dmg := Cint(GetAttribute(caster, "magery") / 2);
        var diff := Resisted(info.circle, caster, cast_on, dmg);
        if(diff)
            if(diff > GetMana(cast_on))
              SetMana(cast_on, 0);
            else
              SetMana(cast_on, GetMana(cast_on) - diff);
            endif
        endif
        return 1;

endprogram