use uo;
use os;
use util;

include ":attributes:attributes";
include ":timedScripts:timedScripts";
include ":magery:spells";
include ":charclasses:classes";

program cast_bless( parms )
    var caster := parms[1];
    var info := parms[2];
    var cast_on;

    cast_on := MS_Target(caster, info, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HELPFUL);
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := parms[4];
    endif

    if (!cast_on)
        return 0;
    endif
    if ( !MS_MobileCheck(caster, cast_on) )
        return 0;
    endif
    if (!CheckCastSpot(caster, info))
        return 0;
    endif
	if ( GetObjProperty(cast_on, "Bless") )
        SendSysMessage(cast_on,"Already under the influence!");
		return 0;
	endif
	MS_PlaySpellSFX(info.spell_id, cast_on);
	MS_PlaySpellGFX(info.spell_id, cast_on, caster);


    var EvalInt := AP_GetSkill(caster, EVALUATING_INTELLIGENCE);
    var Magery := AP_GetSkill(caster, MAGERY);
	if(caster.IsA(POLCLASS_NPC))
        EvalInt := 150;
        Magery := 150;
	endif

    ///cast_on := Reflected( caster, cast_on, info.circle );


	// Str increase % = (Eval / 10 + 1)
	var percent := CDbl(EvalInt / 10) + 1;
	var points := CInt(Magery * (percent / 100));

	// Duration: ((Eval / 5) + 1) * 6
	var duration := ((EvalInt / 5) + 1) * 6;


    if(GetObjProperty(caster,"classid") == ID_MAGE)
        var val := (info.classlevel * 10);
        //points := RandomIntMinMax(val, (val + 10));
        points := GetBuffAmount( caster, info );
        duration := 60 * val;
    endif

	// Same as casting agility, cunning and strength all at once.
	// Bless should probably not be able to stack with Agility, etc. So we do it this way.
	TS_StartTimer(cast_on, "Bless", duration, points, caster);

	return 1;

endprogram

