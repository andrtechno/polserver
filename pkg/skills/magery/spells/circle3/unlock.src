use os;
use uo;
use util;
use cliloc;

include ":magery:spells";
include ":attributes:attributes";
//include ":keys:key";

program cast_strength( parms )
    var caster := parms[1];
    var info := parms[2];
	var orig_lock_id;
    var cast_on := MS_Target(caster, info, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_NEUTRAL);
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := parms[4];
    endif
    if (!cast_on)
        return 0;
    endif
	if (!cast_on.IsA(POLCLASS_CONTAINER))
		SendSysMessage(caster, "That is not a container.", 3, MESSAGE_COLOR_BAD);
		return 0;
	elseif( cast_on.IsLeatherContainer() )
		SendSysMessage( caster, "You cannot lock leather containers.", 3, MESSAGE_COLOR_BAD );
		return 0;
	elseif( cast_on.IsA(POLCLASS_SPELLBOOK) || cast_on.IsA(POLCLASS_CORPSE) )
		SendSysMessage(caster, "You cannot lock that.", 3, MESSAGE_COLOR_BAD);
		return 0;
	endif
	if (!cast_on.locked)
		SendSysMessage(caster,"That is not locked!", 3, MESSAGE_COLOR_BAD);
		return;
	endif
    if (!CheckCastSpot(caster, info))
        return 0;
    endif
// Do not know what the authors were thinking with this.
// I would think Magic Unlock wouldn't be concerned if a container
// was lockable. After all we are using magick here :)
/*	if ( !KP_IsLockable(targ) )
		SendSysMessage(caster, "That is not lockable.");
		return 0;
	endif
*/
	if ( cast_on.locked )
		// Note: No lock levels, but you could if ( CInt(GetObjProperty(cast_on, "LockLevel")) <= 1 )
		if ( GetObjProperty(cast_on, "MagicLock") != error )
			PlaySoundEffect(caster, SFX_SPELL_UNLOCK);
			orig_lock_id := GetObjProperty( cast_on, "OrigLockID" );
			EraseObjProperty(cast_on, "MagicLock");
			EraseObjProperty(cast_on, "MLockPickDiff");
			EraseObjProperty(cast_on, "LockID");
			SendSysMessageCL( caster, 502076, color := 66 ); // The lock quickly yields to your skill.
		    //PrintTextAbovePrivate(cast_on, "*Unlocked!*", caster, color := 66);
		    PrintTextAbovePrivateCL(caster, cast_on, 3005143, color := 66); // // Unlocked
		    if( orig_lock_id )
                SetObjProperty( cast_on, "LockID", orig_lock_id );
            endif

			cast_on.locked := 0;
			SetObjProperty( cast_on, "MagUnlockedBySerial", caster.serial );
		endif
	endif
	
	return 1;

endprogram
