use os;
use uo;
use util;

include ":attributes:attributes";
include ":magery:spells";
include "include/client";
include "include/sounds";

program cast_WEAKEN( parms )
    var caster := parms[1];
    var info := parms[2];
    var cast_on;
    cast_on := MS_Target(caster, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HARMFUL);
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := parms[4];
    endif
    if (!cast_on)
        SendSysMessage (caster, "Cancelled.", color := 33);
        return 0;
    endif
    if ( !MS_MobileCheck(caster, cast_on) )
        return 0;
    endif
  SetObjProperty(cast_on, "LastHit", {caster.name,caster.serial, "weaken" });
  PlaySoundEffect(cast_on, SFX_SPELL_WEAKEN);
  PlayObjectCenteredEffect( cast_on, FX_CURSE_EFFECT, 7,7);
    
  if(Reflected(cast_on))
	SendSysMessage (caster, "The spell was reflected back onto you!!!", color := 33);
	cast_on := caster;
  endif
  if(!CanMod(cast_on, "str"))
	return;
  endif
  var magery := AP_GetSkill(caster, MAGERY);
  var mod_amount := Resisted(info.circle,caster,cast_on,(GetModAmount( magery )));
  var duration := GetModDuration( magery );
  DoTempMod(cast_on, "str", 0 - mod_amount, duration);
endprogram
