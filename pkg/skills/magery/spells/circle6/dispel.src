use vitals;
use uo;
use cfgfile;

include "include/eventID";
include ":attributes:attributes";
include ":magery:spells";
include "include/spellAttack";
//include "include/statMod";

var npccfg := ReadConfigFile("npcdesc");


program dispel ( parms )
    var caster := parms[1];
    var info := parms[2];
    var cast_on;

    cast_on := MS_Target(caster, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HELPFUL);
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := parms[4];
    endif
    if (!cast_on)
        SendSysMessage (caster, "Cancelled.", color := 33);
        return 0;
    endif
    if ( !MS_MobileCheck(caster, cast_on) )
        return 0;
    endif

  var magery := GetAttribute(caster, MAGERY);
  var summoned := GetObjProperty( cast_on, "summoned");
  if(summoned == 1)
    PlaySoundEffect( caster, 0x202 );
    PlayStationaryEffect( cast_on.x, cast_on.y, cast_on.z, FX_HEAL_EFFECT, 0xa, 0xa, 0, cast_on.realm );
    var master := SystemFindObjectBySerial(CInt(GetObjProperty(cast_on, "master")));
    if(master)
      var resist := GetAttribute(master, RESISTING_SPELLS);
      if((RandomInt(99) + 1) < resist)
        MoveObjectToLocation(cast_on,0,0,0, "britannia", MOVEOBJECT_FORCELOCATION);
        ApplyRawDamage(cast_on, GetMaxHp(cast_on) + 3);
      endif
      send_attack(caster, cast_on, 41);
    endif
  else

    if (CanMod(cast_on, "#cast_bless"))
        var bless := GetObjProperty(cast_on, "#cast_bless");
        undo_mod(cast_on, "#cast_bless", bless.value);
    endif
    if (CanMod(cast_on, "#cast_cunning"))
        var int := GetObjProperty(cast_on, "#cast_cunning");
        undo_mod(cast_on, "#cast_cunning", int.value);
    endif
    if (CanMod(cast_on, "#cast_agility"))
        var dex := GetObjProperty(cast_on, "#cast_agility");
        undo_mod(cast_on, "#cast_agility", dex.value);
    endif
    if (CanMod(cast_on, "#cast_strangth"))
        var str := GetObjProperty(cast_on, "#cast_strangth");
        undo_mod(cast_on, "#cast_strangth", str.value);
    endif
    if (CanMod(cast_on, "#cast_protection"))
        var ar := GetObjProperty(cast_on, "#cast_protection");
        undo_mod(cast_on, "#cast_protection", ar.value);
    endif
    if (CanMod(cast_on, "#cast_poly"))
        var poly := GetObjProperty(cast_on, "#cast_poly");
        undo_mod(cast_on, "#cast_poly", poly.value);
    endif
  	SendSysMessage(cast_on,"All the magical effects active on this creature have been wiped.");

    if(cast_on.isA(POLCLASS_NPC))
      var element := npccfg[cast_on.npctemplate].elemental;
      if(element == "undead")
        PlayStationaryEffect( cast_on.x, cast_on.y, cast_on.z, FX_HEAL_EFFECT, 0xa, 0xa, 0, cast_on.realm );
        var ev := array;
        ev.+ type;
        ev.+ source;
        ev.type := EVID_PEACEMADE;
        SendEvent(cast_on, ev);
        ev.source := caster;
        SendEvent(cast_on, ev);
        ev.type := EVID_ENGAGED;
        SendEvent(cast_on, ev);
        PlaySoundEffect(caster, 0x202);
        PlayObjectCenteredEffect(cast_on, 0x37b9, 0x03, 0x0a);
        var dmg := CalcSpellDamage(info.circle, caster, cast_on) + (RandomInt(20));
        ApplyRawDamage(cast_on, Resisted(info.circle, caster, cast_on, dmg));
      endif
    endif
  endif
endprogram