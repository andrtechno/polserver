use uo;
use os;
use basic;
use math;

include "include/direction";

program timer(params)
    var who := params[1];
    var critter := params[2];
    var range := params[3];
    var tracker := params[4];
    var timer := 0;

    while((Distance(who, critter) <= range) && (critter.dead == 0) && (timer <= 12)&& (tracker == GetObjProperty(who,"TrackingCounter")))
        timer := timer + 1;

        var x := critter.x;
        var y := critter.y;
        var z := Abs(critter.z);

        if(z > 0)
            x -= (z / 10);
            y -= (z / 10);
        endif


        SendQuestArrow(who, x, y, "tracking");

        var dir := Get_direction(who, critter);
        if(dir == "nearby")
            SendSysMessage(who, critter.name + " is " + dir);
        else
            SendSysMessage(who, critter.name + " is to the " + dir);
        endif
        sleepms(1000);

    endwhile;
    SendQuestArrow(who, -1, -1, "tracking");
    SendSysMessage(who, "You have lost your quarry.");
    EraseObjProperty(who, "#trackingtimeout");
    return 1;
endprogram
