use uo;
use unicode;

include ":attributes:attributes";
include ":detecthidden:detectHidden";
include ":traps:traps";
include "include/client";

program skill_DetectHidden( mobile )

    if( !(SkillCheck( mobile, DETECTING_HIDDEN, -1 ) > 0) && !mobile.multi )
        SendSysMessage( mobile, "You do not detect anything." );
        return 0;
    endif

    var radius := CInt( AP_GetSkill( mobile, DETECTING_HIDDEN ) / 15 );

    var hiders := ListMobilesNearLocationEx( mobile.x, mobile.y, mobile.z, CInt( radius ), LISTEX_FLAG_HIDDEN, mobile.realm ),
    mobiles_found := 0;

    foreach hider in hiders
        if( hider.serial != mobile.serial )
            // Skill check against hidden's hiding skill
            if( DetectHidden( mobile, hider ))

                mobiles_found += 1;
                PrintTextAbovePrivateUC(hider, "Обнаружен!", "RUS", mobile, 6, MESSAGE_COLOR_GOOD, JOURNAL_UC_PRINT_NAME);
                if(mobile.hidden)
                    PrintTextAbovePrivateUC(mobile, "Вы обнаружены!", "RUS", mobile, 6, MESSAGE_COLOR_BAD, JOURNAL_UC_PRINT_NAME);
                endif
            else
                SendSysMessage( hider, mobile.name+" is trying to discover you!", color := 40 );
            endif
        endif
        SleepMS(5);
    endforeach

    var items := ListItemsNearLocation( mobile.x, mobile.y, mobile.z, CInt( radius ), mobile.realm );
    foreach item in items
        if( !IsTrapRevealed( item ))
            var reaveal := RevealTraps( item );
            if( reaveal )
                PrintTextAbove( item, "*Trapped*", color := 33 );
            endif
        endif
        SleepMS(5);
    endforeach

    if( !mobiles_found )
        //SendSysMessage( mobile, "You do not find anyone hiding." );
        SendSysMessageUC( mobile, "У вас не получилось никого найти..", "RUS", color := MESSAGE_COLOR_BAD );
    endif

	return 1;
endprogram
