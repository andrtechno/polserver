
use uo;
use util;
use cfgfile;
use unicode;

include ":attributes:attributes";
include "include/client";
include "include/objtype";
include "include/trainingskill";
include "include/math";

const MINKARMA := 4000;
const MINFAME := 4000;
const MONUTES := 5;

var zPleas := {
  "Я так голоден, помогите пожалуйста!",
  "Подайте на пропитание!",
  "Мне надо кормить своих четверых детей помогите!",
  "У меня сгорел дом, помогите чем можете!"
};

var zFails := {
  "Уходите пожалуйста!",
  "Чтоб я вас больше не видел!",
  "Извените, ничем не могу помочь!",
  "Не тревожьте больше меня!"
};

var zBroke := {
  "Ain't got nothin' t' give ya! привет",
  "No' got nothin'. Sorry.",
  "Not much better off meself'.",
  "Maybe ye can spare a coin for me?"
};

var zEmpty := {
  "Sorry, gave it all away already.",
  "Ain't got nothin' t' give ya!",
  "Nope. Gots nothing. Sorry.",
  "Your cohorts cleaned me out already!"
};

var zNone := {
  "I'm sorry, I can give thee nothing. привет3",
  "Does it look like I have pockets in this armor?",
  "Not while I'm on duty."
};

var zLowKarma := {
  "I will not give money to such as thee!",
  "Take off, eh?",
  "I do not give money to naughty evildoers!",
  "Thou dost not look trustworthy... no gold for thee today!"
};
const UOBJ_GOLDCOIN :=0x0eed;

program skill_begging( who )
    //SendSysMessage( who, "Select the victim" );
    SendSysMessageUC(who, "У кого просить милостыни?", "RUS", 3, MESSAGE_COLOR_GOOD);

    EraseObjProperty(who, "IsMeditating");
    EraseObjProperty(who, "HealTimer");
    var data := {};
    data[1] := "begging";
    data[2] := ReadGameClock();
    data[3] := SKILLID_BEGGING;
    SetObjProperty(who, "LastUsedSkill", data );
    TrainingSkill(who, data[3]);

    var tgt := Target( who );
    if ( !tgt )
        SendSysMessage( who, "Cancelled" );
        return;
    endif
    if(tgt.serial == who.serial)
        SendSysMessage(who, "You might want to consider therapy.");
        return;
    endif
    if((tgt.graphic != CID_HUMAN_MALE) && (tgt.graphic != CID_HUMAN_FEMALE))
        SendSysMessage(who, "You look a little silly doing that.");
        return;
    endif
    if(GetObjProperty(tgt, "LoggedIn"))
        SendSysMessage(who, "Perhaps talking to them would be a better idea.");
        return;
    endif

    var cfgtemplate := ReadConfigFile("::npcdesc");
    var ismob := tgt.isa(POLCLASS_MOBILE);
    /*if (ismob)
        PrintTextAbove(who, "*looks at " + tgt.name + "*" );
    else
        PrintTextAbove( who, "*looks at " + tgt.desc + "*" );
    endif*/
    if ((!ismob) || (!tgt.isa(POLCLASS_NPC)))
        return;
    endif

    if (ismob.gender)
        PrintTextAboveUC(who, "Простите... Мужчина!", "RUS", 3, MESSAGE_COLOR_GOOD);
    else
        PrintTextAboveUC(who, "Простите... Девушка!", "RUS", 3, MESSAGE_COLOR_GOOD);
    endif
    PlaySoundEffect(who, SFX_58);
    PerformAction(who, ANIM_BOW);
    sleep(3);


    PrintTextAboveUC(who, "Помогите чем можете!", "RUS", 3, MESSAGE_COLOR_GOOD);
    PlaySoundEffect(who, SFX_58);
    PerformAction(who, ANIM_BOW);
    sleep(3);

    PrintRandomMessage( who, zPleas, MESSAGE_COLOR_GOOD);
    PlaySoundEffect(who, SFX_58);
    PerformAction(who, ANIM_BOW);
    sleep(3);

    PrintTextAboveUC(who, "Пааажалуйста!", "RUS", 3, MESSAGE_COLOR_GOOD);
    PlaySoundEffect(who, SFX_58);
    PerformAction(who, ANIM_BOW);
    sleep(3);

    var lastBegging := CInt(GetObjProperty(tgt, "LastBegging"));
    if(lastBegging)
        if(ReadGameClock() < lastBegging)
            PrintTextAboveUC(tgt, "Эйй, я не могу так часто раздавать добро, приходите позже!", "RUS", 3, MESSAGE_COLOR_BAD);
            return;
        endif
    endif


    //PrintRandomMessage( who, zPleas );
    //sleep(1);
    var karma := GetObjProperty(who, "Karma");
    if (!karma)
        karma := 0;
        SetObjProperty(who, "Karma", karma);
    endif
    if (karma < 0)
        PrintRandomMessage( tgt, zLowKarma );
        return;
    endif
    var wealth := cfgtemplate[tgt.npctemplate].wealth;
    if (!wealth)
        PrintTextAbovePrivate(who, "This creature will not give thee money!", who);
        return;
    endif
    var lb := Cint(GetObjProperty(tgt, "begpurse"));
    if(!lb)
        lb := 50;
        SetObjProperty(tgt, "begpurse", lb);
    endif
    //if(lb < 25)
    //  PrintRandomMessage( tgt, zEmpty );
    //  return;
    //endif

    var pts := CInt(GetAttribute(who, BEGGING)) + 15;

    if(!SkillCheck(who,BEGGING, -1, pts/3) > 0)
        PrintRandomMessage( tgt, zFails, MESSAGE_COLOR_BAD);
        SendSysMessageUC(who, "У вас не чего не получилось", "RUS", 3, MESSAGE_COLOR_BAD);
        return;
    endif
    var gold;
    case (wealth)
        "upper":
            gold := RandomDiceRoll("4d5");
        "middle":
            gold := RandomDiceRoll("3d4");
        "poor":
            gold := RandomDiceRoll("2d3");
        "broke":
        default:
            PrintRandomMessage( tgt, zBroke );
            return;
        "none":
            PrintRandomMessage( tgt , zNone );
            return;
    endcase
    if ((GetObjProperty(who, "Karma") > MINKARMA) and (GetObjProperty(who, "Fame") > MINFAME))
        gold := CInt(gold*1.3);
    endif
    lb := Cint(lb - gold);
    SetObjProperty(tgt, "begpurse", lb);
    //PrintTextAbove( tgt, "*throws a few coins to " + who.name + "*" );


    var genderText := "Бедная женщина";
    if(who.gender)
        genderText := "Бедный мужчина";
    endif


    PrintTextAboveUC(tgt, "Ооо! "+genderText+"!", "RUS", 3, MESSAGE_COLOR_GOOD);
    sleep(2);
    PrintTextAboveUC(tgt, "Вы тронули моё сердце вашей историей", "RUS", 3, MESSAGE_COLOR_GOOD);
    sleep(2);
    var premium := who.IsPremium();
    var foodChange := 50;
    var percentGold := 0;
    case(premium)
        1: percentGold := 25; foodChange := subtract_percent(foodChange, 10);
        2: percentGold := 50; foodChange := subtract_percent(foodChange, 20);
        3: percentGold := 75; foodChange := subtract_percent(foodChange, 30);
    endcase
    if(foodChange >= RandomInt(99)+1)
        PrintTextAboveUC(tgt, "Возьмите это!", "RUS", 3, MESSAGE_COLOR_GOOD);
        sleep(2);
        var flist := {0x097A, 0x097B, 0x097C, 0x09C9, 0x09D0, 0x09D1, 0x09D2, 0x09EA};
        CreateItemInBackpack( who, flist[RandomInt(flist.size())+1], 1 );
        SendSysMessageUC(who, "Вы получили еду!", "RUS", 3, MESSAGE_COLOR_GOOD);
    else
        PrintTextAboveUC(tgt, "Возьмите эту скромную сумму!", "RUS", 3, MESSAGE_COLOR_GOOD);
        sleep(2);


        if(percentGold)
            gold := add_percent(gold, percentGold);
        endif
        print("original % "+gold);
        if ( !CreateItemInBackpack( who, UOBJ_GOLDCOIN, gold ) )
            SendSysMessage( who, "You miss and the coins bounce into cracks in the ground!" );
        else
            var coinText := "золотых монет";
            if (gold == 1)
                coinText := "золотую монету";
            endif
            SendSysMessageUC(who, "Вы получили " + gold + " " + coinText + "!", "RUS", 3, MESSAGE_COLOR_GOOD);
        endif
    endif

    SetObjProperty(tgt, "LastBegging", ReadGameClock() + (MONUTES * 60));
    if (GetObjProperty(who, "Karma") > 10)
        SendSysMessage(who, "You lose a little karma.");
        SetObjProperty(who, "Karma", GetObjProperty(who, "Karma")-10);
    endif

    var fame := GetObjProperty(who, "Fame");
    if (!fame)
        fame := 0;
        SetObjProperty(who, "Fame", fame);
    endif
    if (fame > 5)
        SendSysMessage(who, "You lose a little fame.");
        SetObjProperty(who, "Fame", CInt(fame*0.98));
    endif
    return;
endprogram

function PrintRandomMessage( speaker, byref zSpeech, color := MESSAGE_COLOR_MESSAGE )
    if(zSpeech.size())
        PrintTextAboveUC(speaker, zSpeech[RandomInt(zSpeech.size())+1], "RUS", 3, color);
    endif
    return;
endfunction
