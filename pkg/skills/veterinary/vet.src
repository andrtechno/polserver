use uo;
use util;
use cfgfile;
use os;

include "include/client";
include "include/math";
include ":attributes:attributes";
include ":timedScripts:poisonUtil";

var config;
var elem;

var clock := ReadGameClock();

program use_bandages (parms)
    var patient := parms[1];
    var who := parms[2];
    var bandage := parms[3];
    EraseObjProperty(who, "IsMeditating");

    if(Distance(who, patient) > 2)
        SendSysMessage(who, "Your patient is too far away.", color := 33);
        return;
    elseif(!CheckLineOfSight(who,patient))
	    SendSysMessage(who,"You cant see your patient.", color := 33);
	    return;
    elseif((AP_GetVital(patient, VITALID_LIFE) >= AP_GetStat(patient, STRENGTH)) && (TS_GetTimerNames(patient, "P").size() == 0))
        SendSysMessage(who,"Your patient is at full heath", color := 66);
        return;
    endif
    Detach();
    SetObjProperty(who, "HealTimer", clock);
    if(TS_GetTimerNames(patient, "P").size() > 0)
        if(!CheckLineOfSight(who,patient))
            SendSysMessage(who,"You can't see your patient.", color := 33);
            return;
        endif
        if(Distance(who, patient) <= 2)
            SendSysMessage(who,"You start to cure "+patient.name+".");
            cure(who, patient, bandage);
        else
            SendSysMessage(who, "Your patient is too far away.", color := 33);
            return;
        endif
    else
        if(!CheckLineOfSight(who,patient))
            SendSysMessage(who,"You can't see your patient.", color := 33);
            return;
        endif
        if(Distance(who, patient) <= 2)
            SendSysMessage(who,"You start to heal "+patient.name + ".", color := 66);
            heal(who, patient, bandage);
        else
            SendSysMessage(who, "Your Patient is too far away.", color := 33);
            return;
        endif
    endif
endprogram

function heal(who, patient, bandy)
    var chp;
    var healing := AP_GetSkill(who, VETERINARY);
	if(bandy.IsEnhancedBandage())
		healing := healing + CInt(GetObjProperty(bandy, "HealingBonus"));
	endif

    var animallore := (CInt(AP_GetSkill(who, ANIMAL_LORE)) / 5);
    var healamt := CInt(healing + animallore + 10);
    var counter := 0;


    if(!GetObjProperty(who, "HealTimer"))
        SendSysMessage(who, "You stop healing your patient.", color := 33);
        return;
    elseif(clock != GetObjProperty(who, "HealTimer"))
        SendSysMessage(who, "You stop healing your patient to work on another.", color := 88);
        return;
    endif
    chp := AP_GetVital(who, VITALID_LIFE);
    sleep(5);
    if(Distance(who, patient) > 2)
        SendSysMessage(who,"Your patient isn't close enough.", color := 33);
        return;
    endif
    if(who.warmode)
        SendSysMessage(who,"You stop healing your patient.", color := 33);
        return;
    endif
    if(AP_GetVital(who, VITALID_LIFE) < chp)
        if((RandomInt(100)+1) > who.dex)
            SendSysMessage( who, "Your fingers slip.", color := 33 );
            healamt := (healamt / 2);
        endif
    endif
    if(TS_GetTimerNames(patient, "P").size() > 0)
        cure(who, patient, bandy);
        return;
    endif

    if(SkillCheck( who, VETERINARY, 10, 100 ) > 0)
        if(healamt <= 10)
            SendSysMessage(who,"You finish applying the bandages, but they barely help.", color := 88);
        endif
        var premium := who.IsPremium();
        case(premium)
            1: healamt := add_percent(healamt, 10);
            2: healamt := add_percent(healamt, 20);
            3: healamt := add_percent(healamt, 30);
        endcase
        if((patient.str - AP_GetVital(patient, VITALID_LIFE)) > healamt)
            HealDamage(patient, healamt);
            AwardRawSkillPoints(who, SKILLID_VETERINARY, (healamt * 2));
        else
            //healamt := AP_GetStat(patient, STRENGTH) - AP_GetVital(patient, VITALID_LIFE);
            HealDamage(patient, healamt);
            AwardRawSkillPoints(who, SKILLID_VETERINARY, (healamt * 2));
        endif
        if(AP_GetVital(patient, VITALID_LIFE) == AP_GetVitalMaximumValue(patient, VITALID_LIFE))
            SendSysMessage(who, "You have healed your patient completely.", color := MESSAGE_COLOR_GOOD);
        else
            if(healamt <= Cint(AP_GetVitalMaximumValue(patient, VITALID_LIFE)))
                SendSysMessage(who, "You successfully heal " + patient.name+" on " + CStr(healamt) + " points.", color := MESSAGE_COLOR_GOOD);
            else
                SendSysMessage(who, "You successfully heal " + patient.name+".", color := MESSAGE_COLOR_GOOD);
            endif
	    endif
        return;
    else
        SendSysMessage( who, "You could not heal your patient.", color := 33 );
    endif
endfunction

function cure(who, patient, bandy)
    var chp;
    var healing := AP_GetSkill(who, VETERINARY);
    if(bandy.IsEnhancedBandage())
		healing := healing + 10;
	endif
    healing := (CInt(healing) / 2);
    var animallore := (CInt(AP_GetSkill(who, ANIMAL_LORE)) / 5);
    var healamt := CInt(healing + animallore + 10);


    if(!GetObjProperty(who, "HealTimer"))
        SendSysMessage(who, "You stop healing your patient.", color := 33);
        return;
    elseif(clock != GetObjProperty(who, "HealTimer"))
        SendSysMessage(who, "You stop healing your patient to work on another.", color := 88);
        return;
    endif
    chp:=AP_GetVital(who, VITALID_LIFE);
    sleep(5);
    if(Distance(who, patient) > 1)
        SendSysMessage(who,"Your patient isn't close enough.", color := 33);
        return;
    endif
    if(who.warmode)
        SendSysMessage(who,"You stop healing your patient.", color := 33);
        return;
    endif
	if(AP_GetVital(who, VITALID_LIFE) < chp)
        if((RandomInt(100)+1) > who.dex)
	        SendSysMessage( who, "Your fingers slip.", color := 33 );
            healamt := ((healamt * 3) / 4);
        endif
	endif

    if((SkillCheck( who, VETERINARY, 80, 0 ) > 0))
        if((RandomInt(100) + 1) < healamt)
            TS_CurePoison(patient, healamt/40, "P", 1);
            if (TS_GetTimerNames(patient, "P").size() > 0)
                SendSysMessage(who, "You have partially cured your patient.", color := 88);
            else
                SendSysMessage(who, "You have cured your patient completely.", color := MESSAGE_COLOR_GOOD);
            endif
        else
            SendSysMessage(who,"You finish applying the bandages, but they barely help.", color := 88);
        endif
        return;
    else
        SendSysMessage( who, "You could not heal it.", color := 33 );
    endif
endfunction
