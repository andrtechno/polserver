use uo;
use util;
use os;
use cfgfile;

include ":attributes:attributes";
include "include/spellAttack";
include ":poisonwatcher:poisons";
include ":damage:damage";

program field_control(item)
  sleep(1);
  var npccfg := ReadConfigFile("::npcdesc");
  var boss := SystemFindObjectBySerial(GetObjProperty(item, "creater"), SYSFIND_SEARCH_OFFLINE_MOBILES);
  SetScriptController(boss);
  var dmg := CInt(GetAttribute(boss, MAGERY) / 25);
  if(!dmg)
    dmg := 1;
  endif
  while(item)
    foreach character in ListMobilesNearLocationEX(item.x, item.y, item.z, 0, LISTEX_FLAG_NORMAL + LISTEX_FLAG_HIDDEN, item.realm)
    if((!character.dead) and (!character.cmdlevel))
        PlaySoundEffect(character, 0x206);
        if(character.isA(POLCLASS_NPC))
          if((npccfg[character.npctemplate].elemental) == "poison")
            HealDamage(character, (dmg * 10));
          else
            send_attack(character, boss, 46);
            ApplyRawDamage(character, 1); // We'll be mean to NPCs and not take into account their poison resist.
            HealDamage(character, 1);
            var passparms := {character, boss, "poisoned weapon", dmg, 1};
            start_script(":spells:poisonDamage", passparms);
          endif
        else
          ApplyRawDamageEX(character, 1, DMG_POISON);
          HealDamage(character, 1);
          var passparms := {character, boss, "poison field", dmg, 1};
          start_script(":spells:poisonDamage", passparms);
        endif
    endif
    endforeach
    sleepms(1500);
  endwhile
endprogram